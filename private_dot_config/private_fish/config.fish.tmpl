#
# Set up home directory for Volta.
#
set -gx volta_home '/users/tni/.volta'

#
# Add directories to PATH.
#
set -U fish_user_paths $fish_user_paths ~/.cargo/bin '$volta_home/bin'

{{- if eq .chezmoi.os "linux" }}

if status --is-login
  #
  # Starts our window manager.
  #
  if test -z "$DISPLAY"
    exec startx -- -keeptty
  end
end
{{- end }}

if status --is-interactive

  function __start_tmux
    {{- if eq .chezmoi.os "linux" }}
    #
    # Do not start tmux when in the Linux virtual console. This is the terminal
    # that comes with the kernel, which we are dropped into on boot, and we'll
    # start a window manager from it instead of a terminal multiplexer.
    #
    if test "$TERM" = "linux"
      return 0
    end
    {{- end }}

    #
    # We don't want to nest tmux sessions, so start tmux only when not already
    # in a tmux session.
    #
    if not set -q TMUX
      if command -v tmux > /dev/null
        set -l session_name tmux
        exec tmux new-session -A -s $session_name
      end
    end
  end

  #
  # Start tmux when possible and drop into it.
  #
  __start_tmux

  #
  # Sets up the prompt to look like:
  #
  #   [12:27] directory (master) |
  #
  function fish_prompt
    #
    # Print the time of day in a muted color.
    #
    set_color brblack
    echo -n '['(date "+%H:%M")'] '

    #
    # Print the current directory name.
    #
    set_color yellow
    if test "$PWD" != "$HOME"
      echo -n (basename $PWD)
    else
      echo -n '~'
    end

    #
    # Print Git information, when available.
    #
    set_color green
    printf '%s ' (__fish_git_prompt)

    #
    # Print the prompt symbol.
    #
    set_color red
    echo -n '| '

    set_color normal
  end

  #
  # Sets up the greeting to look like:
  #
  #   Good afternoon, Teddy.
  #
  function fish_greeting
    set -l nickname Teddy

    echo
    set_color green

    echo -n '    '

    set -l hour (date +%H)
    if test "$hour" -lt 12
      echo -n 'Good morning, '
    else if test "$hour" -lt 18
      echo -n 'Good afternoon, '
    else
      echo -n 'Good evening, '
    end
    echo "$nickname."

    echo
    set_color normal
  end

  #
  # Prefer the neovim fork of vim.
  #
  #   https://neovim.io/
  #
  abbr -a    vi nvim
  abbr -a   vim nvim
  setenv EDITOR nvim

  #
  # Consistency when opening a file.
  #
  # From the terminal:
  #   e
  #
  # From vim:
  #   :e
  #
  abbr -a e nvim

  #
  # Prefer exa, which is "a modern replacement for ls".
  #
  #   https://the.exa.website/
  #
  if command -v exa > /dev/null
    abbr -a ls 'exa'
    abbr -a ll 'exa -l'
  else
    abbr -a ll 'ls -l'
  end

  #
  # Prefer bat, which is a better version of cat.
  #
  #   https://github.com/sharkdp/bat
  #
  if command -v bat > /dev/null
    abbr -a cat 'bat'
  end

  #
  # Setup autojump, a "cd command that learns". Its command from the shell is
  # mapped to "j".
  #
  #   https://github.com/wting/autojump
  #
  if test -f /usr/local/share/autojump/autojump.fish;
    source /usr/local/share/autojump/autojump.fish;
  end

  #
  # Add color to man pages.
  #
  #   http://linuxtidbits.wordpress.com/2009/03/23/less-colors-for-man-pages/
  #
  setenv LESS_TERMCAP_mb \e'[01;31m'       # begin blinking
  setenv LESS_TERMCAP_md \e'[01;38;5;74m'  # begin bold
  setenv LESS_TERMCAP_me \e'[0m'           # end mode
  setenv LESS_TERMCAP_se \e'[0m'           # end standout-mode
  setenv LESS_TERMCAP_so \e'[38;5;246m'    # begin standout-mode - info box
  setenv LESS_TERMCAP_ue \e'[0m'           # end underline
  setenv LESS_TERMCAP_us \e'[04;38;5;146m' # begin underline

  {{- if eq .chezmoi.os "linux" }}

  #
  # Start keychain for ssh-agent management.
  #
  keychain --eval --quiet -Q --nogui --noask id_rsa_github_tjni | source

  {{- end }}
end
